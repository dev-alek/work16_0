block-level on error undo, throw.
/*

$Revision$
$Author$
$Date$
$Workfile$
$Archive$

Триггер на запись раскладки

Автор: Бахтадзе Наталья Викторовна
Дата создания: 09/26/08
Author: Bakhtadze Natalya
Creation date: 09/26/08

*/

TRIGGER PROCEDURE FOR WRITE OF ub.layout OLD old-layout.

define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Триггер на запись раскладки".
{ cmp/vssrevis.i "substitute('&1'
                         , ub.layout.layout-id
                                                  ) " }

{ cmp/trg-def.i }
{ gbl/cur-time.i }
DEFINE VARIABLE v-today as date no-undo .
DEFINE VARIABLE v-time as integer no-undo .
define variable v-cmp as logical no-undo .
define buffer buf_c-layout for ub.c-layout.

main-block:
do
on error  undo main-block, return error substitute( "&1. &2&3&4", vss-workfile, return-value, {&new-line}, error-status :get-message (1))
on stop   undo main-block, return error substitute( "&1. stop", vss-workfile )
on endkey undo main-block, return error substitute( "&1. endkey", vss-workfile )
:

  /*запись в историю надо осуществлять в редакторе раскладки*/
  if ub.layout.is-default > 0
  and g#db-num > 0
  and not g#news
  then do:
    undo main-block, return error substitute("&1. Нельзя добавлять/изменять эталонную раскладку в УБД", vss-workfile ).
  end.

  if not g#news
  then do:
    if new (ub.layout) then do:
      assign
      ub.layout.cr-db-num = g#db-num.
    end.
    assign
    ub.layout.whole-send-news = 0.
    run str/callnews.p
      (input {&table_layout}
      ,input (buffer ub.layout:handle)
      ).
  end.
  if g#oxml = yes
  then do:
    run str/calloxml.p (
          input {&nwsdochs_action_update}
        , input {&table_layout}
        , input ( buffer ub.layout:handle )
    ) no-error.
    if error-status :error
    then do:
        undo, return error substitute( "&2&1Ошибка при отправке записи в систему OpenXML&1&3&1&4"
                             , {&new-line}
                             , vss-workfile
                             , return-value
                             , error-status :get-message ( 1 ) ).
    end.
  end.
end. /*doe*/