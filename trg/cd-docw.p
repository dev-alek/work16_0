block-level on error undo, throw.
/*

$Revision$
$Author$
$Date$
$Workfile$
$Archive$

Триггер на запись документа на кассе

Автор: Бахтадзе Наталья Викторовна
Дата создания: 01/22/07
Author: Bakhtadze Natalya
Creation date: 01/22/07

*/

TRIGGER PROCEDURE FOR WRITE OF ub.cd-doc old old_cd-doc.
define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Триггер на запись документа на кассе ".
{ cmp/vssrevis.i "substitute('&1|&2|&3|&4|&5'
                                    ,ub.cd-doc.obj-type
                                    ,ub.cd-doc.obj-code
                                    ,ub.cd-doc.pos-type
                                    ,ub.cd-doc.doc-type
                                    ,ub.cd-doc.doc-code
                                          ) " }

{ cmp/trg-def.i  }
{ gbl/cur-time.i }


define variable v-date as date no-undo .
define variable v-time as integer no-undo .
define variable v-db-num as integer no-undo .
define variable v-changes as character no-undo .

define buffer buf_c-cash-desk for ub.c-cash-desk.
define buffer buf_c-cd-doc for ub.c-cd-doc.
define buffer buf_cash-desk for ub.cash-desk.

main-block:
do
on error  undo main-block, return error substitute( "&1. &2&3&4", vss-workfile, return-value, {&new-line}, error-status :get-message ( 1 ) )
on stop   undo main-block, return error substitute( "&1. stop", vss-workfile )
on endkey undo main-block, return error substitute( "&1. endkey", vss-workfile )
:

  if not g#news then do:
    { gbl/objdbnum.i ub.cd-doc.obj-type ub.cd-doc.obj-code v-db-num }
    if v-db-num <> g#db-num then do:
      message
      "Нельзя изменять запись об клиенте на кассе," skip
      "принадлежащей другой БД"
      view-as alert-box .
      undo, return error.
    end.
  end.
  buffer-compare old_cd-doc to ub.cd-doc
  case-sensitive
  save result in v-changes.
  if trim(replace(replace(v-changes, 'to-del', ''), 'to-send', '':U), {&comma-char}) = '':U then return.
  if not g#news then do:
    run cur-time in this-procedure(output v-date, output v-time).
    create buf_c-cd-doc.
    assign
    buf_c-cd-doc.obj-type           = ub.cd-doc.obj-type
    buf_c-cd-doc.obj-code           = ub.cd-doc.obj-code
    buf_c-cd-doc.pos-type           = ub.cd-doc.pos-type
    buf_c-cd-doc.doc-type           = ub.cd-doc.doc-type
    buf_c-cd-doc.doc-code           = ub.cd-doc.doc-code
    buf_c-cd-doc.chip-num           = next-value (s-cash-desk-chip, {&db-name_schema})
    buf_c-cd-doc.corr-time          = v-time
    buf_c-cd-doc.corr-user-db-num   = g#db-num
    buf_c-cd-doc.corr-user-name     = g#userid
    buf_c-cd-doc.corr-date          = v-date
    .
  end.
  if ub.cd-doc.charkey_one = {&fact} then do:
    run str/callnews.p (  input {&table_cd-doc}
                        ,input (buffer ub.cd-doc:handle)
                      ) no-error .
    if error-status:error then undo, return error return-value  .
  end.

  if g#oxml = yes
  then do:
    run str/calloxml.p (
          input {&nwsdochs_action_update}
        , input {&table_cd-doc}
        , input ( buffer ub.cd-doc:handle )
    ) no-error.
    if error-status :error
    then do:
        undo, return error substitute( "&2&1Ошибка при отправке записи в систему OpenXML&1&3&1&4"
                             , {&new-line}
                             , vss-workfile
                             , return-value
                             , error-status :get-message ( 1 ) ).
    end.
  end.
end.