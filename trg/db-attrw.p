/*
$Revision$
$Author$
$Date$
$Workfile$
$Archive$

Триггер на запись db-attr

Автор: Уханов Дмитрий Юрьевич
Дата создания: 03/22/04
Author: Dmitry Ukhanov
Creation date: 03/22/04

*/

TRIGGER PROCEDURE FOR WRITE OF ub.db-attr.


define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Триггер на запись db-attr".
{ cmp/vssrevis.i }
{ cmp/trg-def.i  }
{ gbl/db-attr.i  }
define variable p-news as logical no-undo.

main-block:
do
on error  undo main-block, return error substitute( "&1. &2&3&4", vss-workfile, return-value, {&new-line}, error-status :get-message ( error-status :num-messages ) )
on stop   undo main-block, return error substitute( "&1. stop", vss-workfile )
on endkey undo main-block, return error substitute( "&1. endkey", vss-workfile )
:

  run db-attr-news in this-procedure
    ( input ub.db-attr.attr-code
     ,output p-news
    ) no-error.

  if p-news = true then do:
    run str/callnews.p
      (input {&table_db-attr}
      ,input (buffer ub.db-attr:handle)
      ) no-error .
    if error-status :error then do:
      undo, return error substitute( "&1. Невозможно маршрутизировать db-attr для отправки в новости. &2&3&2&4", vss-workfile, {&new-line}, return-value, error-status :get-message ( error-status :num-messages ) ).
    end.
  end.

  if g#oxml = yes
  then do:
    run str/calloxml.p (
          input {&nwsdochs_action_update}
        , input {&table_db-attr}
        , input ( buffer ub.db-attr:handle )
    ) no-error.
    if error-status :error
    then do:
        undo, return error substitute( "&2&1Ошибка при отправке записи в систему OpenXML&1&3&1&4"
                             , {&new-line}
                             , vss-workfile
                             , return-value
                             , error-status :get-message ( 1 ) ).
    end.
  end.
end.

/* $Workfile$ end */