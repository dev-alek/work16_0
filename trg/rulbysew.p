block-level on error undo, throw.
/*

$Revision$
$Author$
$Date$
$Workfile$
$Archive$

Триггер на привязку правила к набору правил

Автор: Бахтадзе Наталья Викторовна
Дата создания: 09/19/06
Author: Bakhtadze Natalya
Creation date: 09/19/06

*/

TRIGGER PROCEDURE FOR WRITE OF ub.rule-by-set OLD old_rule-by-set.

define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Триггер на привязку правила к набору правил".
{ cmp/vssrevis.i "substitute('&1|&2|&3'
                         , ub.rule-by-set.codex_id
                         , ub.rule-by-set.ruleset_id
                         , ub.rule-by-set.rule_id
                         ) " }

{ cmp/trg-def.i }
{ gbl/cur-time.i }
{ gbl/key-rec.i }

define variable v-uniq-key-rec as character no-undo .
define variable v-date as date no-undo .
define variable v-time as integer no-undo .
define buffer buf_c-rule for ub.c-rule.
define buffer buf_c-rule-by-set for ub.c-rule-by-set.


main-block:
do
on error  undo main-block, return error substitute( "&1. &2&3&4", vss-workfile, return-value, {&new-line}, error-status :get-message (1))
on stop   undo main-block, return error substitute( "&1. stop", vss-workfile )
on endkey undo main-block, return error substitute( "&1. endkey", vss-workfile )
:

  if not g#news
  or g#db-num <> 0
  then do:
    run cur-time in this-procedure ( output v-date, output v-time).
    create buf_c-rule-by-set.
    assign
    buf_c-rule-by-set.codex_id           = ub.rule-by-set.codex_id
    buf_c-rule-by-set.ruleset_id         = ub.rule-by-set.ruleset_id
    buf_c-rule-by-set.rule_id            = ub.rule-by-set.rule_id
    buf_c-rule-by-set.chip-num           = next-value (s-ref-corr-chip, {&db-name_schema})
    buf_c-rule-by-set.corr-time          = v-time
    buf_c-rule-by-set.corr-user-db-num   = g#db-num
    buf_c-rule-by-set.corr-user-name     = (if g#news
                                    then {&nts-user}
                                    else g#userid)
    buf_c-rule-by-set.corr-date          = v-date
    .
    create buf_c-rule.
    buffer-copy buf_c-rule-by-set
    to buf_c-rule
    assign
    buf_c-rule.action             = integer(if new(ub.rule-by-set) then {&hn-create} else {&hn-update})
    buf_C-rule.subject            = {&table_rule-by-set}
    .
  end.
  if not g#news
  and g#db-num = 0
  then do:
    run str/callnews.p
      (input {&table_rule-by-set}
      ,input (buffer ub.rule-by-set:handle)
      ).
  end.
    if g#oxml = yes
    then do:
    run str/calloxml.p (
          input {&nwsdochs_action_update}
        , input {&table_rule-by-set}
        , input ( buffer ub.rule-by-set:handle )
    ) no-error.
    if error-status :error
    then do:
        undo, return error substitute( "&2&1Ошибка при отправке записи в систему OpenXML&1&3&1&4"
                             , {&new-line}
                             , vss-workfile
                             , return-value
                             , error-status :get-message ( 1 ) ).
    end.
    end.
end. /*doe*/
