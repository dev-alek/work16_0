/*
$Revision: $
$Author: $
$Date: $
$Workfile: $
$Archive: $

Печать слипов чека

*/

define input parameter p-db-num as integer no-undo .
define input parameter p-ID as character no-undo .
define input parameter p-CheckId as character no-undo .
define input parameter p-RRN as character no-undo .
define input parameter p-print-type as character no-undo .

define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Печать слипов чека".
{ cmp/vssrevis.i }

define variable v-slip-txt as character no-undo .
define variable v-slip-txt-list as character no-undo .
define variable cmd as character no-undo .
define stream out-slip .

define buffer chk-slip-head for ub.chk-slip-head .
define buffer chk-slip-string for ub.chk-slip-string .

define variable v-file-name as character no-undo.
define variable vok as logical no-undo.
define variable ii as integer no-undo .

SYSTEM-DIALOG GET-FILE v-file-name
    TITLE "Сохранить как"
    FILTERS
      " Файл PDF(*.pdf) " "*.pdf",
      " Все файлы (*.*) " "*.*"
    ask-overwrite
    save-as
    use-filename
    update vok
    default-extension "pdf"
    .
if not vok THEN do:
  return .
end.

case p-print-type :
  when "one"
  then do :
    v-slip-txt = "slip_" + p-ID .
    output stream out-slip to value(v-slip-txt) convert target "UTF-8" .
      for each chk-slip-string no-lock where chk-slip-string.db-num = p-db-num
                                         and chk-slip-string.ID = p-ID
                                         and chk-slip-string.CheckID = p-CheckId
                                         and chk-slip-string.RRN = p-RRN
                                         by chk-slip-string.str-num
                                         :
        put stream out-slip unformatted chk-slip-string.str-value skip .
      end .   
    output stream out-slip close .
    
    cmd = substitute('&1 -n="&2" -o="&3"', search("exe/slip2pdf.exe"), search(v-slip-txt), v-file-name) .
  end .
  when "all"
  then do :
    v-slip-txt-list = "" .
    for each chk-slip-head no-lock where chk-slip-head.db-num = p-db-num
                                     and chk-slip-head.CheckID = p-CheckId
                                     :
      v-slip-txt = "slip_" + chk-slip-head.ID .
      output stream out-slip to value(v-slip-txt) convert target "UTF-8" .
        for each chk-slip-string no-lock where chk-slip-string.db-num = chk-slip-head.db-num
                                           and chk-slip-string.ID = chk-slip-head.ID
                                           and chk-slip-string.CheckID = chk-slip-head.CheckId
                                           and chk-slip-string.RRN = chk-slip-head.RRN
                                           by chk-slip-string.str-num
                                           :
          put stream out-slip unformatted chk-slip-string.str-value skip .
        end .   
      output stream out-slip close . 
      v-slip-txt-list = v-slip-txt-list + search(v-slip-txt) + "," .                                 
    end .
    v-slip-txt-list = trim(v-slip-txt-list, ",") .
    
    cmd = substitute('&1 -n="&2" -o="&3"', search("exe/slip2pdf.exe"), v-slip-txt-list, v-file-name) .
  end .
  when "all_pay"
  then do :
    v-slip-txt-list = "" .
    for each chk-slip-head no-lock where chk-slip-head.db-num = p-db-num
                                     and chk-slip-head.CheckID = p-CheckId
                                     and chk-slip-head.RRN = p-RRN
                                     :
      v-slip-txt = "slip_" + chk-slip-head.ID .
      output stream out-slip to value(v-slip-txt) convert target "UTF-8" .
        for each chk-slip-string no-lock where chk-slip-string.db-num = chk-slip-head.db-num
                                           and chk-slip-string.ID = chk-slip-head.ID
                                           and chk-slip-string.CheckID = chk-slip-head.CheckId
                                           and chk-slip-string.RRN = chk-slip-head.RRN
                                           by chk-slip-string.str-num
                                           :
          put stream out-slip unformatted chk-slip-string.str-value skip .
        end .   
      output stream out-slip close . 
      v-slip-txt-list = v-slip-txt-list + search(v-slip-txt) + "," .                                 
    end .
    v-slip-txt-list = trim(v-slip-txt-list, ",") .
    
    cmd = substitute('&1 -n="&2" -o="&3"', search("exe/slip2pdf.exe"), v-slip-txt-list, v-file-name) .
  end .
end case .

os-command silent value(cmd) .

if p-print-type = "one"
then do :
  os-delete value(v-slip-txt) no-error .
end .
else do :
  do ii = 1 to num-entries(v-slip-txt-list) :
    v-slip-txt = entry(ii, v-slip-txt-list) .
    os-delete value(v-slip-txt) no-error .
  end .
end .



