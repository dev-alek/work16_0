/*

$Revision$
$Author$
$Date$
$Workfile$
$Archive$

Экспорт во Внешнюю Бухгалтерию документов и суммарного расхода по чекам

Автор: Хныкин Павел Андреевич
Дата создания: 09/09/05
Author: Pavel Khnykin
Creation date: 09/09/05

Input:

Output:

*/
define input parameter p-mainmenu-handle    as handle           no-undo.
define input parameter p-date-from          as date             no-undo. /* начало периода экспорта */
define input parameter p-date-to            as date             no-undo. /* конец  периода экспорта */
define input parameter p-range              as integer          no-undo. /* Диапазон: 1 - глобально, 2 - по тек. фирме, 3 - список объектов */
define input parameter p-obj-list           as character        no-undo. /* Список объектов для p-range = 3 */
define input parameter p-doc-type-list      as character        no-undo. /* Список типов операций */
define input parameter p-pay-code           as logical          no-undo. /* надо ли экспортировать кассовые платежи */
define input parameter p-cst                as logical          no-undo. /* надо ли экспортировать ГТД из parts */
define input parameter p-parts              as logical          no-undo. /* надо ли экспортировать parts */
define input parameter p-chk-pay-code       as logical          no-undo. /* надо ли экспортировать разброс по типам касс платежей*/
define input parameter p-pay-desk           as logical          no-undo. /* надо ли экспортировать разброс по кассам */
define input parameter p-pay-desk-cards     as logical          no-undo. /* надо ли экспортировать разброс по префиксам карт */
define input parameter p-deleted            as logical          no-undo. /* надо ли экспортировать удаленные документы */
define input parameter p-chk                as logical          no-undo. /* надо ли экспортировать чеки */
define input parameter p-opened-docs        as logical          no-undo. /* надо ли экспортировать не закрытые документы */
define input parameter hedt                 as handle           no-undo.
define input parameter hcnt                 as handle           no-undo.

define variable vss-revision    as character no-undo init "$Revision$":U .
define variable vss-author      as character no-undo init "$Author$":U .
define variable vss-date        as character no-undo init "$Date$":U .
define variable vss-workfile    as character no-undo init "$Workfile$":U .
define variable vss-archive     as character no-undo init "$Archive$":U .
define variable vss-description as character no-undo init "Экспорт во Внешнюю Бухгалтерию документов и суммарного расхода по чекам".
{ cmp/vssrevis.i }
{ cmp/str-glbl.i }
{ cmp/library.i  }
{ gbl/temphost.i }
{ bge/bge-xml.i  }
{ gbl/getcntxt.i def }

&scoped-define version-string "15.0 " + replace( vss-revision + vss-date, "$", " " )

    define variable v-xml-file-name     as character            no-undo. /* имя файла вывода */
    define variable v-log-file-name     as character            no-undo. /* имя log-файла */
    define variable v-locked            as logical              no-undo.
    define variable v-log-string        as character            no-undo. /* имя log-файла */
    define variable v-fact-order-from   like ub.stk-tot.fact-order no-undo.
    define variable v-fact-order-to     like ub.stk-tot.fact-order no-undo.
    define variable v-docs-exists       as logical              no-undo.
    define variable v-obj-counter       as integer              no-undo.
    define variable v-db-num            as integer              no-undo.
    define variable v-yesno             as logical              no-undo.
    define variable v-obj-num           as character            no-undo.
    define variable v-obj-list          as character            no-undo.
do
on error undo, return error return-value
:
    run bge-xml-init-ext-doc-type in this-procedure .
    { gbl/getcntxt.i get " " p-mainmenu-handle }
    { gbl/chk-actg.i
    v-cntxt-db-num
    v-cntxt-userid
    {&action-head-code-main}
    'actn_documents_export':U
    {&cntxt-firm}
    v-cntxt-host-code-obj
    '':U
    0
    0
    0
    0
    yes
    v-yesno
    }
    if v-yesno = no
    then do:
        return error.
    end.
    RUN init-temphost.
    assign
        v-log-string = ", по всем фирмам"
    .
    case p-range:
    when 2      /* Экспорт по текущей фирме */
    then do:
        for each temp-obj
        where temp-obj.host-code <> v-cntxt-host-code-obj
        :
            delete temp-obj.
        end.
        assign
            v-log-string = ", по фирме (код фирмы " + string( v-cntxt-host-code-obj ) + ")"
        .
    end.
    when 3      /* Экспорт по списку объектов */
    then do:
        for each temp-obj
        :
            delete temp-obj.
        end.
        do v-obj-counter = 1 to num-entries ( p-obj-list ) / 2
        :
            create temp-obj.
            assign
                temp-obj.obj-type = entry( v-obj-counter * 2 - 1, p-obj-list )
                temp-obj.obj-code = integer( entry( v-obj-counter * 2, p-obj-list ) )
            no-error .
            if error-status :error
            then do:
                message
                         vss-workfile vss-revision vss-description
                    skip(1)
                    skip "Ошибка чтения списка объектов"
                    skip return-value
                    skip trim( error-status :get-message( 1 ) )
                         trim( error-status :get-message( 2 ) )
                         trim( error-status :get-message( 3 ) )
                view-as alert-box error.
                undo, return error substitute( "Ошибка чтения списка объектов. &1. &2"
                                            , return-value
                                            , trim( error-status :get-message( 1 ) ) ).
            end.
            { gbl/hostcode.i temp-obj.obj-type temp-obj.obj-code temp-obj.host-code no-error }
            if error-status :error
            then do:
                message
                         vss-workfile vss-revision vss-description
                    skip(1)
                    skip "Не найдена фирма для объекта" temp-obj.obj-type temp-obj.obj-code
                    skip return-value
                    skip trim( error-status :get-message( 1 ) )
                         trim( error-status :get-message( 2 ) )
                         trim( error-status :get-message( 3 ) )
                view-as alert-box error.
                undo, return error substitute( "Не найдена фирма для объекта &1 &2. &3. &4"
                                , temp-obj.obj-type
                                , temp-obj.obj-code
                                , return-value
                                , trim( error-status :get-message( 1 ) ) ).
            end.
        end.
        assign
            v-log-string = ", по объектам: " + p-obj-list
        .

    end.
    end case.
    run bge-xml-read-config in this-procedure ( input p-date-to
                                              , input ?
                                              ) no-error.
    if error-status :error
    then do:
        message
        vss-workfile vss-revision vss-description
        skip "Ошибка чтения параметров экспорта."
        skip "Для экспорта данных будут приняты параметры по умолчанию."
        skip return-value
        skip trim(error-status :get-message(1))
            trim(error-status :get-message(2))
            trim(error-status :get-message(3))
        view-as alert-box error.
    end.
      run xml-bge-filename in this-procedure (
            input "doc"
          , input "document"
          , input "no"
          , output v-xml-file-name
          , output v-log-file-name
          , output v-locked
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input "&DLine"
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "Начало выгрузки в файл &1"
                                  , replace( v-xml-file-name, "/", "\" ) + "xm1"
                            )
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "................с параметрами: Дата с: &1, дата по: &2"
                                  , p-date-from
                                  , p-date-to
                            )
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "................с параметрами: ... список объектов: &1", p-obj-list )
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "................с параметрами: ... типы документов: &1", p-doc-type-list )
      ).
      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "................с параметрами: ... надо ли виды платежей: &1, ГТД: &2, партии: &3, типы кассовых платежей: &4, кассы: &5, преф.карт: &6, удал.документы: &7, незакр.документы: &8"
                                                        , p-pay-code
                                                        , p-cst
                                                        , p-parts
                                                        , p-chk-pay-code
                                                        , p-pay-desk
                                                        , p-pay-desk-cards
                                                        , p-deleted
                                                        , p-opened-docs )
      ).
      if v-locked = yes
      then do:
          run wp-XMLWriteLog in this-procedure (
                input v-log-file-name
              , input 1
              , input "*** Ошибка выгрузки: Файл выгрузки заблокирован другим процессом."
          ).
          message
                  vss-workfile vss-revision vss-description
              skip(1)
              skip "*** Ошибка выгрузки: Файл выгрузки заблокирован другим процессом."
              skip return-value
              skip trim( error-status :get-message( 1 ) )
                  trim( error-status :get-message( 2 ) )
                  trim( error-status :get-message( 3 ) )
          view-as alert-box error.
          undo, return error substitute( "*** Ошибка выгрузки: Файл выгрузки заблокирован другим процессом." ).
      end.
      { gbl/curdbnum.i
          v-db-num
      no-error }
      if error-status :error
      then do:
          run wp-XMLWriteLog in this-procedure (
                input v-log-file-name
              , input 1
              , input "*** Не удалось определить номер БД. Номер текущей базы данных будет выгружен как 0."
          ).
          assign
              v-db-num = 0
          .
      end.
      run bge-xml-write-header in this-procedure (
            input v-xml-file-name
          , input v-xml-file-name + "xml"
          , input {&version-string}
          , input v-db-num
          , input p-date-from
          , input 0
          , input p-date-to
          , input 0
          , input p-obj-list
          , input p-doc-type-list
          , input p-pay-code
          , input p-cst
          , input p-parts
          , input p-chk-pay-code
          , input p-pay-desk
          , input p-pay-desk-cards
          , input p-deleted
          , input p-opened-docs
      ).
      object-of-list:
      for each temp-obj
      :
          run export-docs-by-object (
                input temp-obj.host-code
              , input temp-obj.obj-type
              , input temp-obj.obj-code
          ) no-error.
          if error-status :error
          then do:
              run wp-XMLWriteLog in this-procedure (
                    input v-log-file-name
                  , input 1
                  , input substitute( "Ошибка экспорта документов по объекту &1 &2"
                                      , temp-obj.obj-type
                                      , temp-obj.obj-code
                                      )
              ).
              message
              vss-workfile vss-revision vss-description
              skip "Ошибка экспорта документов по объекту"
              skip "Тип объекта:" temp-obj.obj-type
              skip "Код объекта:" temp-obj.obj-code
              skip return-value
              skip trim(error-status :get-message(1))
                  trim(error-status :get-message(2))
                  trim(error-status :get-message(3))
                  trim(error-status :get-message(4))
                  trim(error-status :get-message(5))
              view-as alert-box error.
              next object-of-list.
          end.
          run cb-fill_bge-xml_clients in this-procedure (
                input temp-obj.obj-type
              , input temp-obj.obj-code
          ).
      end.

      if v-bge-xml-bgeflold <> "oracle"
      then do:
        run xml-bge-write-footer in this-procedure ( input v-xml-file-name ).
      end.

      run wp-XMLWriteLog in this-procedure (
            input v-log-file-name
          , input 1
          , input substitute( "Данные выгружены в файл &1"
                                  , replace( v-xml-file-name, "/", "\" ) + "xml"
                            )
      ).
    run bge/cat-firm.p (
          input "list":U
        , input v-cntxt-host-code-obj
        , input table temp_bge-xml_clients
    ).
    run bge/cat-good.p (
          input "good-ext,list"
        , input table temp_bge-xml_goods
    ).
    run wp-XMLWriteLog in this-procedure (
          input v-log-file-name
        , input 1
        , input "&DLine"
    ).
end.

/*==========================================================================*/
procedure export-docs-by-object :
define input parameter p-host-code  as integer      no-undo.
define input parameter p-obj-type   as character    no-undo.
define input parameter p-obj-code   as integer      no-undo.

    define variable v-archive-ok        as logical      no-undo.
    define variable v-comment           as character    no-undo.
    define variable v-err-message       as character    no-undo.
do
on error undo, return error return-value
:
    run wp-XMLWriteEDT( hEDT, 1, string( p-obj-type ) + " " + string( p-obj-code ) ).

/*---S-------- Расчет архивов на объекте ------------------*/
    run wp-XMLWriteEDT( hEDT, 4,  "Расчет архивов").

    run bge/bge-ahz.p (
          input p-mainmenu-handle
        , input p-obj-type
        , input p-obj-code
        , input yes
        , input yes
        , input no
        , input p-date-from
        , input p-date-to
        , output v-archive-ok
        , output v-comment
    ) no-error.
    if error-status :error
    then do:
        run wp-XMLWriteLog in this-procedure (
              input v-log-file-name
            , input 1
            , input substitute( "Ошибка проверки архивов на объекте &1 &2. &3. &4"
                                    , p-obj-type
                                    , p-obj-code
                                    , return-value
                                    , trim( error-status :get-message( 1 ) ) )
        ).
        message
                 vss-workfile vss-revision vss-description
            skip(1)
            skip "Ошибка проверки архивов на объекте"
            skip "Тип объекта:" p-obj-type
            skip "Код объекта:" p-obj-code
            skip return-value
            skip trim( error-status :get-message( 1 ) )
                 trim( error-status :get-message( 2 ) )
                 trim( error-status :get-message( 3 ) )
        view-as alert-box error.
        undo, return error substitute( "Ошибка проверки архивов на объекте &1 &2. &3. &4"
                                    , p-obj-type
                                    , p-obj-code
                                    , return-value
                                    , trim( error-status :get-message( 1 ) ) ).
    end.
    if v-archive-ok = no
    then do:
        assign
            v-err-message =  substitute( "*** Документы по объекту &1 &2 в интервале дат &3 - &4 не будут выгружены. Архивы по объекту в заданном интервале дат не целостные. &5."
                                    , p-obj-type
                                    , p-obj-code
                                    , p-date-from
                                    , p-date-to
                                    , v-comment       )
        .
        run wp-XMLWriteLog in this-procedure (
              input v-log-file-name
            , input 1
            , input v-err-message
        ).
        message
        vss-workfile vss-revision vss-description
        skip v-err-message
        skip return-value
        skip trim(error-status :get-message(1))
            trim(error-status :get-message(2))
            trim(error-status :get-message(3))
        view-as alert-box error.
        undo, return error v-err-message.
    end.
    process events.
/*---E-------- Расчет архивов на объекте ------------------*/
/*---S----- Границы fact-order для дат dFrom - dTo --------*/
    run rep/get-fo.p (
          input  p-obj-type
        , input  p-obj-code
        , input  p-date-from
        , input  p-date-to
        , output v-fact-order-from
        , output v-fact-order-to
        , output v-docs-exists
    ) no-error .
    if error-status :error
    then do:
        message
                 vss-workfile vss-revision vss-description
            skip(1)
            skip "Ошибка определения границ fact-order для поиска по архивам"
            skip return-value
            skip trim( error-status :get-message( 1 ) )
                 trim( error-status :get-message( 2 ) )
                 trim( error-status :get-message( 3 ) )
        view-as alert-box error.
        undo, return error substitute( "Ошибка определения границ fact-order для поиска по архивам. &1. &2"
                                    , return-value
                                    , trim( error-status :get-message( 1 ) ) ).
    end.
    if v-docs-exists = no
    then do:
        run wp-XMLWriteEDT( hEDT, 4, "В заданном диапазоне дат нет закрытых документов").
    end.        /* if v-docs-exists = no */
    else do:
        run wp-XMLShowCNT(hCNT).
/*---E----- Границы fact-order для дат dFrom - dTo --------*/
        for each temp_ext-doc-type
        :
            if lookup( temp_ext-doc-type.ext-doc-type, p-doc-type-list ) <> 0
            or p-doc-type-list = "":U
            then do:
                run bge/doc-oper.p (
                      input p-host-code
                    , input p-obj-type
                    , input p-obj-code
                    , input temp_ext-doc-type.ext-doc-type
                    , input temp_ext-doc-type.ext-doc-type-label
                    , input v-fact-order-from
                    , input v-fact-order-to
                    , input p-date-from
                    , input p-date-to
                    , input p-pay-code
                    , input p-cst
                    , input p-parts
                    , input p-chk-pay-code
                    , input p-pay-desk
                    , input p-pay-desk-cards
                    , input p-chk
                    , input v-xml-file-name
                    , input v-log-file-name
                    , input this-procedure
                    , input hEDT
                    , input hCNT
                ) no-error.
                if error-status :error
                then do:
                    run wp-XMLWriteEDT( hEDT, 1, substitute( "&1. &2. &3."
                                            , return-value
                                            , trim(error-status :get-message(1))
                                            , trim(error-status :get-message(2))
                                            )  ).
                end.
            end.
        end.        /* for each temp_ext-doc-type */
        run wp-XMLHideCNT(hCNT).
    end.        /* NOT ( if v-docs-exists = no ) */
    if v-bge-xml-bgeflold = "oracle":u
    then do:
      run wp-XMLShowCNT in this-procedure (hCNT).
      run bge/doc-ord.p ( input no
                        , input p-host-code
                        , input p-obj-type
                        , input p-obj-code
                        , input p-date-from
                        , input p-date-to
                        , input {&o-p}
                        , input {&ord-rcv}
                        , input v-xml-file-name
                        , input v-log-file-name
                        , input this-procedure
                        , input hEDT
                        , input hCNT
                        , input ?
                        ) no-error .
      if error-status :error
      then do:
          run wp-XMLWriteEDT( hEDT, 1, substitute( "&1. &2. &3."
                                  , return-value
                                  , trim(error-status :get-message(1))
                                  , trim(error-status :get-message(2))
                                  )  ).
      end.
      run wp-XMLHideCNT in this-procedure (hCNT).
    end.
    if p-deleted = yes
    then do:
        run wp-XMLShowCNT(hCNT).
        run bge/doc-deld.p (
              input p-host-code
            , input p-obj-type
            , input p-obj-code
            , input p-date-from
            , input p-date-to
            , input v-xml-file-name
            , input v-log-file-name
            , input hEDT
            , input hCNT
        ) no-error.
        if error-status :error
        then do:
            run wp-XMLWriteEDT( hEDT, 1, substitute( "&1. &2. &3."
                                    , return-value
                                    , trim(error-status :get-message(1))
                                    , trim(error-status :get-message(2))
                                    )  ).
        end.
        run wp-XMLHideCNT(hCNT).
    end.
    process events.
    if p-opened-docs = yes
    then do:
        run wp-XMLShowCNT(hCNT).
        run bge/doc-opnd.p (
              input p-host-code
            , input p-obj-type
            , input p-obj-code
            , input p-cst
            , input p-parts
            , input v-xml-file-name
            , input v-log-file-name
            , input this-procedure
            , input hEDT
            , input hCNT
        ) no-error.
        if error-status :error
        then do:
            run wp-XMLWriteEDT( hEDT, 1, substitute( "&1. &2. &3."
                                    , return-value
                                    , trim(error-status :get-message(1))
                                    , trim(error-status :get-message(2))
                                    )  ).
        end.
        run wp-XMLHideCNT(hCNT).
    end.
    process events.

end.
end procedure. /* export-docs-by-object */


/*==========================================================================*/
procedure cb-fill_bge-xml_goods :
define input parameter p-gds-code   as integer          no-undo.

do
on error undo, return error
:
    find first temp_bge-xml_goods
         where temp_bge-xml_goods.gds-code = p-gds-code
    no-error.
    if not available temp_bge-xml_goods
    then do:
        create temp_bge-xml_goods.
        assign
            temp_bge-xml_goods.gds-code = p-gds-code
        .
    end.
end.
end procedure. /* cb-fill_bge-xml_goods */


/*==========================================================================*/
procedure cb-fill_bge-xml_clients :
define input parameter p-obj-type   as character        no-undo.
define input parameter p-obj-code   as integer          no-undo.

    define buffer buf_temp_bge-xml_clients      for temp_bge-xml_clients.
do
for buf_temp_bge-xml_clients
on error undo, return error
:
    find first buf_temp_bge-xml_clients
         where buf_temp_bge-xml_clients.obj-type = p-obj-type
           and buf_temp_bge-xml_clients.obj-code = p-obj-code
    no-error.
    if not available buf_temp_bge-xml_clients
    then do:
        create buf_temp_bge-xml_clients.
        assign
            buf_temp_bge-xml_clients.obj-type = p-obj-type
            buf_temp_bge-xml_clients.obj-code = p-obj-code
        .
    end.
end.
end procedure. /* cb-fill_bge-xml_goods */